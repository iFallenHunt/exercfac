O problema consiste em ler informações (código, descrição, preço e quantidade em estoque) de 20 produtos. Além disso, deve ser permitido executar quantas consultas o operador desejar. Desse modo, digitando o código do produto, apresentam-se a ele o preço e o saldo em estoque. Se o código digitado não existir, o usuário deve ser informado. Sistematizando o problema, temos que:
* Objetivo do algoritmo: ler informações sobre 20 produtos e permitir a consulta pelo código do produto.
* Entrada: ler o código, a descrição, o preço e a quantidade em estoque de 20 produtos e o código para pesquisa.
* Processamento: encontrar o produto com o código informado pelo usuário.
* Saída: imprimir o preço e o saldo em estoque do produto, se existir o código informado. Senão, informar ao usuário que não há produto cadastrado com o código digitado.
O quadro a seguir apresenta o algoritmo para o problema descrito. Observe que esse algoritmo envolve o conceito de registros, laço de repetição e busca em vetor.

Algoritmo registroproduto

Tipo

	Cad_produto = registro
		codigo: inteiro
		nome: Vetor[1..30] de caractere
		preco: real
		saldo: inteiro
	Fim_registro

Var

	produto: Vetor[1..10] de Cad_produto
	i, codigo: inteiro
	acha: lógico
	resp: Vetor[1..3] de caractere

Início

	Para i de 1 até 20 faça
		Escreva(“Informe código do produto:”)
		Leia(produto[i].codigo)
		Escreva(“Informe a descrição do produto:”)
		Leia(produto[i].nome)
		Escreva(“Informe o preço do produto:”)
		Leia(produto[i].preco)
		Escreva(“Informe o saldo em estoque do produto:”)
		Leia(produto[i].saldo)
	Fim_para
	resp <- “sim”
	Enquanto (resp = “sim”) faça
		Escreva(“Informe o código a ser pesquisado:”)
		Leia(codigo)
		i <- 1
		acha <- falso
		Enquanto (i < = 20) e (acha= falso) faça
			Se (produto[i].codigo = codigo) então
				acha <- verdadeiro
			Senão
				i <- i + 1
			Fim_se
		Fim_enquanto
		Se (acha = verdadeiro) então
			Escreva(“O preço é:”, produto[i].preco)
			Escreva(“O saldo em estoque é:”, produto[i].saldo)
		Senão
			Escreva(“Não há produto com o código informado”)
		Fim_se
		Escreva(“Deseja continuar a pesquisa?”)
		Leia(resp)
	Fim_enquanto
Fim