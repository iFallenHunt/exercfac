Elabore um algoritmo que leia informações (matrícula, nome, lotação e salário) de 30 professores. Deve ser permitido executar quantas consultas o operador desejar, em que ele digita a matrícula e são apresentados a lotação e o salário do professor. Se a matrícula digitada não existir, informar o usuário.
* Objetivo do algoritmo: ler informações sobre 30 professores e realizar consulta pela matrícula.
* Entrada: ler matrícula, nome, lotação e salário e matrícula para pesquisa.
* Processamento: encontrar o professor com a matrícula informada pelo usuário.
* Saída: imprimir a lotação, se existir a matrícula informada. Senão, informar ao usuário que não há professor com a matrícula digitada.

Algoritmo registroprofessor

Tipo

	Cad_professor = registro
		matricula: inteiro
		nome: Vetor[1..30] de caractere
		lotacao: Vetor[1..30] de caractere
		salario: real
	Fim_registro

Var
	
	professor: Vetor[1..30] de Cad_professor
	i, registro: inteiro
	acha: lógico
	resp: Vetor[1..3] de caractere

Início

	Para i de 1 até 30 faça
		Escreva(“Informe a matrícula:”)
		Leia(professor[i].matricula)
		Escreva(“Informe o nome:”)
		Leia(professor[i].nome)
		Escreva(“Informe a lotação:”)
		Leia(professor[i].lotacao)
		Escreva(“Informe o salário:”)
		Leia(professor[i].salario)
	Fim_para
	resp <- “sim”
	Enquanto (resp = “sim”) faça
		Escreva(“Informe a matricula a ser pesquisado:”)
		Leia(registro)
		i <- 1
		acha <- falso
		Enquanto (i <= 20) e (acha = falso) faça
			Se (professor[i].matricula = registro) então
				acha <- verdadeiro
			Senão
				i <- i + 1
			Fim_se
		Fim_enquanto
		Se (acha = verdadeiro) então
			Escreva(“A lotação é:”, professor[i].lotacao)
			Escreva(“O salário é:”, professor[i].salario)
		Senão
			Escreva(“Não há professor com a matrícula informada”)
		Fim_se
		Escreva(“Deseja continuar a pesquisa?”)
		Leia(resp)
	Fim_enquanto
Fim